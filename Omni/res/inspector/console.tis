// ----------------------------------------------------------------------------
class PromptBehavior : Behavior
{
	function attached()
	{
		var nav_arr = [];
		var nav_idx = -1;

		var el_console = this.prior;

		this.$(button).onClick = function() {
			self#snippets[0].ns.el_console = el_console;

			this.popup(self#snippets, Utils.popupAttachment(9, 3));
		};
		this.$(widget).on("keydown", function(e) {
			// command input
			if(e.keyCode==Event.VK_RETURN)
			{
				if(this.value)
				{
					// add to command history
					nav_arr.unshift(this.value);
					nav_idx = -1;

					el_console.EvalInput(this.value);
				}
				this.value = undefined;
		
				return true;
			}

			// up/down history
			else if(e.keyCode==Event.VK_UP)
			{
				if(nav_idx == nav_arr.length-1)
					return;

				nav_idx++;
				this.value = nav_arr[nav_idx];
				this.setSelection(Integer.MAX, Integer.MAX);
			}
			else if(e.keyCode==Event.VK_DOWN)
			{
				if(nav_idx == -1)
					return;

				nav_idx--;
				if(nav_idx==-1)
				{
					this.value = undefined;
					return;
				}
				this.value = nav_arr[nav_idx];
				this.setSelection(Integer.MAX, Integer.MAX);
			}
		});
	}
}


// ----------------------------------------------------------------------------
function GlobalEval(el, cmd)
{
	return el.eval(cmd, el.ns);
}

class OutputBehavior : Behavior
{
	const MAX_ITEMS = 100;
	const SUBSYSTEM = ["dom", "csss", "css", "tis", "console"];
	const SEVERITY = ["info", "warning", "error", "none"];
	const SEVERITY_ICON = [".ft.ft-info", ".ft.ft-attention-1", ".ft.ft-cancel-circled-2", ""];

	function attached()
	{
		var el_this = this;
		this.is_main_console = this.id=="main-console";
		this.is_this_console = this.id=="this-console";

		this.on("click", "#console-menu li", function(evt) {
			switch(evt.target.@#command)
			{
			case "clear":
				this.post(::el_this.Clear());
				break;

			case "copy":
				{
					var opt = el_this.$(option:current);
					if(opt)
						view.clipboard(#put, opt.text);
				}
				break;
			}
			return true;
		});

		this.on("click", "span.data", function(evt) {
			console.show(this.val);
			return true;
		});

		this.on("click", "span.iinspect", function(evt) {
			console.dom(this.val);
			return true;
		});
	}

	function Clear()
	{
		//for(var el in this.$$(*))
		//	el.remove();
		this.clear();// is causing an AV

		if(this.is_main_console)
			this.ResetCounters();
	}

	function EvalInput(txt)
	{
		// get 'this' environment variable
		var el_this;
		if(this.is_this_console)
			el_this = view.Host_GetInspectedElement();
		if(!el_this)
			el_this = view.Host_GetPageRootElement();

		// eval input
		var bOK = true;
		var res;
		try
		{
			res = GlobalEval(el_this, txt);
		} catch(e) {
			bOK = false;
			res = e;
		}

		// show output
		var line = [3, (bOK ? 3 : 2), res];
		this.InsertLines([line]);
	}

	function crackText(text)
	{
		const RE_FILE_LINE = /[(](file:[^()]+[(](\d+)[)])[)]/g;

		text = text.replace("\t", "  ");//.htmlEscape();
		/*text = text.replace(RE_FILE_LINE, function(str,g,n) {
			return "(<a href='" + (g /~ "(") + "' line=" + n + " >" + (g %~ "/") + "</a>)";
		});*/
		return text;
	}

	function valueString(val)
	{
		var composite;
		switch(typeof val)
		{
		case #Element:	composite = "Element " + val.tagname(); break;
		case #Node:
			assert(val.isElement==false);
			composite = "Node " + (val.isText ? "text" : "comment");
			break;
		case #object:	if(val!=null) composite = "Object"; break;
		case #array:	composite = "Array"; break;
		case #Event:	composite = String.printf("%s", val); break;
		case #Image:	composite = String.printf("Image (%dpx, %dpx)", val.width, val.height); break;
		}

		var el_span;
		if(composite)
		{
			if(typeof val==#Element)
			{
				el_span = new Element(#span);
				el_span.attributes.addClass("ielement");
				el_span.html = "<span class='data' /><span class='iinspect' />";
				/*el_span = Element.create{ span, 
					class: "ielement",
					[
						{ span, class: "data", [composite] },
						{ span, class: "iinspect", [{i}] },
					]};*/
				el_span[0].text = composite;
				el_span[0].val = val;
				el_span[1].val = val;
			} else {
				//el_span = Element.create{ span, class: "data", [composite] };
				el_span = new Element(#span);
				el_span.text = composite;
				el_span.attributes.addClass("data");
				el_span.val = val;
			}
		} else {
			var txt;
			switch(typeof val)
			{
				case #date:		txt = "[Date " + val.toISOString() + "]"; break;
				default:		txt = crackText(String.$({val})); break;
			}

			//if(typeof val == #string)
			//	txt = "\"" + txt + "\"";
			el_span = new Element(#span);
			el_span.attributes.addClass("val");
			el_span.text = txt;
			//el_span = Element.create{ span, class: "val", [txt] };
		}
		return el_span;
	}

	function renderRootValue(el_opt, val)
	{
		el_opt.append(valueString(val));

		if(typeof val==#array)
		{
			el_opt.append(" [");
			for(var (i,v) in val)
			{
				el_opt.append(" ");
				el_opt.append(valueString(v));
				el_opt.append(i != val.length-1 ? "," : " ");
			}
			el_opt.append("]");
		}
		else if(typeof val==#object && val!=null)
		{
			el_opt.append(" {");

			var keys = [];
			for(var(k,v) in val)
				keys.push(k);
			keys = keys.sort();

			for(var(i,k) in keys)
			{
				el_opt.append(" " + k + ": ");
				el_opt.append(valueString(val[k]));
				el_opt.append(i != keys.length-1 ? "," : " ");
			}

			el_opt.append("}");
		}
		this.append(el_opt);
	}

	function InsertLines(lines)
	{
		var sx = this.scroll(#left);
		var sy = this.scroll(#bottom);

		for(var item in lines)
		{
			if(this.length >= MAX_ITEMS)
				this.first.remove();

			var subs = item[0];
			var sev = item[1];
			var value = item[2];

			//self.post(::log(SUBSYSTEM[subs], SEVERITY[sev], SEVERITY_ICON[sev]));
			this.append(String.printf("<option class='%s %s'><i %s /></option>", SUBSYSTEM[subs], SEVERITY[sev], SEVERITY_ICON[sev]));
			this.renderRootValue(this.last, value);
		}

		//this.scrollTo(sx, 10000, false /*smooth*/);
		this.post( ::this.scrollTo(sx, 10000) );
	}

	function InsertValues(arr_vals)// console SUBSYSTEM!
	{
		var sx = this.scroll(#left);
		var sy = this.scroll(#bottom);

		if(this.length >= MAX_ITEMS)
			this.first.remove();

		this.append(String.printf("<option class='%s %s'><i %s /></option>", SUBSYSTEM[4], SEVERITY[0], SEVERITY_ICON[0]));
		for(var val in arr_vals)
		{
			this.renderRootValue(this.last, val);
			this.last.append(" ");
		}

		this.scrollTo(sx, 10000, false /*smooth*/);
	}

	// Warn/error counting
	var ns_warn_count = 0;
	var ns_error_count = 0;

	function UpdateCounters(data)
	{
		for(var item in data)
		{
			var subs = item[0];
			var sev = item[1];
			var value = item[2];

			if(SEVERITY[sev]=="warning")
			{
				ns_warn_count++;
				self#topbar.$(.warn span).text = ns_warn_count;
			}
			if(SEVERITY[sev]=="error")
			{
				ns_error_count++;
				self#topbar.$(.error span).text = ns_error_count;
			}
		}
	}

	function ResetCounters()
	{
		ns_warn_count = 0;
		ns_error_count = 0;
		self#topbar.$(.warn span).text = 0;
		self#topbar.$(.error span).text = 0;
	}
}